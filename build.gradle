buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "java"
    id "com.google.protobuf" version "0.8.2"
}

apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.github.igorperikov.heimdallr'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    grpcVersion = '1.5.0'

    springVersion = '5.0.0.RELEASE'

    jsr305Version = '3.0.2'
    slf4jVersion = '1.7.25'
    logbackVersion = '1.2.3'
    lombokVersion = '1.16.18'

    protobufJavaVersion = '3.3.0'
    protocVersion = '3.3.0'

    junitVersion = '5.0.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }

    generatedFilesBaseDir = "$projectDir/src"
}

dependencies {
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"

    compile "org.springframework:spring-core:${springVersion}"
    compile "org.springframework:spring-beans:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"

    compile "com.google.protobuf:protobuf-java:${protobufJavaVersion}"

    compile "com.google.code.findbugs:jsr305:${jsr305Version}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "ch.qos.logback:logback-core:${logbackVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}
